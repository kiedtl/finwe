(use* core)
(use* varvara)

(use dampe)

(struct Maybe0 (Any) [value $0])//{{{

#(method Maybe0) (word maybe? ((AnyOf Maybe0) -- Bool) [
	:value 0<>
])

#(method Maybe0) (word maybe-not? ((AnyOf Maybe0) -- Bool) [
	:value 0=
])

#(method Maybe0) (word unwrap ((AnyOf Maybe0) -- (FieldType $0 value)) [
       :value
       dup 0= (when [ "Unwrapped 0 value" panic ])
])

#(method Maybe0) (word orelse (
	(FieldType $0 value) (AnyOf Maybe0) -- (FieldType $0 value)
) [
       :value dup 0= (when [ drop ] [ nip ])
])

(test Maybe0/maybe? [ 0 (make (Of Maybe0 U8)) ;maybe? (should eq nil) ])
(test Maybe0/maybe-not? [ 0 (make (Of Maybe0 U8)) ;maybe-not? (should eq t) ])
(test Maybe0/orelse [ 0 (make (Of Maybe0 U8)) 12 swap ;orelse (should eq 12) ])
//}}}

// MaybeFF {{{
// Magic value is 0xFFFF for 2-byte types, 0xFF for 1-byte types
//
(struct MaybeFF (Any) [value $0])

#(method MaybeFF) (word maybe? ((AnyOf MaybeFF) -- Bool) [
	:value  (as (USz (FieldType $0 value)))
	0xFFFFs (as (USz (FieldType $0 value))) <>
])

#(method MaybeFF) (word maybe-not? ((AnyOf MaybeFF) -- Bool) [
	:value  (as (USz (FieldType $0 value)))
	0xFFFFs (as (USz (FieldType $0 value))) =
])

#(method MaybeFF) (word unwrap ((AnyOf MaybeFF) -- (FieldType $0 value)) [
	:value dup
	0xFFFFs (as (FieldType $0 value)) = (when [ "Unwrapped FF value" panic ])
])

#(method MaybeFF) (word orelse ( (FieldType $0 value) (AnyOf MaybeFF) -- (FieldType $0 value)) [
       :value dup 0xFFFFs (as (FieldType $0 value)) = (when [ drop ] [ nip ])
])

(test MaybeFF/maybe? [
	0     (make (Of MaybeFF U8)) ;maybe? (should eq t)
	0xFF  (make (Of MaybeFF U8)) ;maybe? (should eq nil)
	0xFFs (make (Of MaybeFF U16)) ;maybe? (should eq t)
])

(test MaybeFF/maybe-not? [ 0xFF (make (Of MaybeFF U8)) ;maybe-not? (should eq t) ])
(test MaybeFF/orelse [ 0xFF (make (Of MaybeFF U8)) 12 swap ;orelse (should eq 12) ])
//}}}

(word itoa10 (AnySigned @U8 --) [
	move
	dup sign 0<> (when [
		'- (r copy) <- (r 1+)
		abs
	] [ (as (USz $1)) ])
	(r move)
	itoa10
])

(word itoa10 (Any @U8 --) [
	move
	dup  9 (as $1) > (when [ (r 1+) ])
	dup 99 (as $1) > (when [ (r 1+) ])
	(until [ 0= ] [
		10 (as $1) /mod
		(as U8) '0 + (r copy) <-
		// TODO: replace with (r 1-) when bug is fixed
		(r [ 1s - (as @U8) ])
	]) drop
	(r drop)
])
(test itoa10 [
	(let buf [U8 6])

	0 6s @buf memset8
	   3 @buf itoa10 @buf print-string (should stdout-eq "3")

	0 6s @buf memset8
	-79i @buf itoa10 @buf print-string (should stdout-eq "-79")

	0 6s @buf memset8
	918s @buf itoa10 @buf print-string (should stdout-eq "918")

	0 6s @buf memset8
	100s @buf itoa10 @buf print-string (should stdout-eq "100")
])

// ( src dst -- dst[src] )
(word strcat ( @[Char8] @[Char8] -- @[Char8] ) [
	(while [ (-- Bool) ldak 0<> ] [ 1+ ])
	move
	ldak (while [ 0<> ] [ (r copy) <- (r 1+) 1+ ldak ]) drop
	0 (r copy) <-
	drop (r move)
])
(test strcat [
	(let buf [U8 24])

	0 24s @buf memset8
	"foo" @buf strcat drop @buf print-string (should stdout-eq "foo")
	"bar" @buf strcat drop @buf print-string (should stdout-eq "foobar")

	0 24s @buf memset8
	""    @buf strcat drop @buf print-string (should stdout-eq "")
	"hi"  @buf strcat drop @buf print-string (should stdout-eq "hi")
	""    @buf strcat drop @buf print-string (should stdout-eq "hi")

	0 24s @buf memset8
	"C"   @buf strcat "++" swap strcat drop
		@buf print-string (should stdout-eq "C++")

	0 24s @buf memset8
	// Test that it adds the null terminator
	"foo\0aaaa" @buf strcat drop @buf print-string (should stdout-eq "foo")
])

// FIXME: hundred ways to optimize this, see ;scmp from left project
// Probably have to wait for (r)/move/copy and friends though.
//
(word strequ ( @[Char8] @[Char8] -- Bool ) [
	2dup -> swap-sb ->
	2dup 0= swap 0= and (when [ 2drop 2drop t return ])   // precheck for empty strs
	(until [ drop 2dup 0= swap 0= and ] [
		<> (when [ 2drop nil return ])
		1+ dup -> move           // ( a* b*+ | b-> )
		swap 1+ dup -> (r move)  // ( b*+ a*+ a-> b-> )
	])
	2drop 2drop
	t
])
(test strequ [
	"windows11" "good" strequ          (should eq nil)
	"" "" strequ                       (should eq t)
	"foo" "foo" strequ                 (should eq t)
	"Hail Zig" "Hail Zig\0zags" strequ (should eq t)
	"Hail Zig2" "Hail Zig"      strequ (should eq nil)
	"foo" "" strequ                    (should eq nil)
])

// ( haystack needle -- haystack-starts-with-needle? )
(word strstartequ ( @[Char8] @[Char8] -- Bool ) [
	move

	(while [
		(-- Bool)
		ldak (r [ ldak move ])
		2dup = rot 0<> rot 0<> and and
	] [
		1+ (r 1+)
	])
	drop
	(r [ move ]) -> 0=
])
(test strstartequ [
	"Hail Zig2" "Hail Zig" strstartequ (should eq t)
	"blah blah" "foo bar"  strstartequ (should eq nil)
	"blah foo"  "foo bar"  strstartequ (should eq nil)
	"foo"       "foo bar"  strstartequ (should eq nil)
	"foo bar"   "foo"      strstartequ (should eq t)
	//""          ""         strstartequ (should eq t)
	"c"         "b"        strstartequ (should eq nil)
	""          "b"        strstartequ (should eq nil)
	"b"         "b"        strstartequ (should eq t)
	"b"         ""         strstartequ (should eq t)
	"foo\0abc"  "foo\0xyz" strstartequ (should eq t)
])

(word strlen ( @[Char8] -- U16 ) [ // TODO: move to str module
	0s swap
	(while [ -> 0<> ] [ swap 1+ swap 1+ ])
	drop
])
(test strlen [
	""                    strlen (should eq  0s)
	"Glory to the Empire" strlen (should eq 19s)
	"Hello, world!"       strlen (should eq 13s)
])

// ( val len ptr -- )
(word memset8 (Any8 U16 AnyPtr16 -- ) [
	(as @U8)
	dup rot + (as @U8)

	(while [ drop 2dup <> ] [
		// ( val ptr end )
		rot-bss>                  // ( end val  ptr  )
		stak                      // ( end val  ptr  )
		1+ rot-sbs                // ( val ptr+ end  )
	])

	3drop
])

(struct ExpansionFillCommand
	[type      U8]
	[len      U16]
	[bank     U16]
	[addr @Opaque]
	[value     U8]
)

// ( len ptr -- )
(word memzero (U16 AnyPtr16 -- ) [
	(let cmdbuf ExpansionFillCommand [0x00 0x0000s 0x0000s 0x0000s 0x00])

	@cmdbuf :addr <-
	@cmdbuf :len  <-

	//0s @cmdbuf :bank  <-
	//0  @cmdbuf :value <-

	@cmdbuf .System/expansion deo
])
(test memset8_and_memzero [
	(let buf [U8 4])
	
	9 4s @buf memset8
	@buf :0 -> (should eq 9)
	@buf :1 -> (should eq 9)
	@buf :2 -> (should eq 9)
	@buf :3 -> (should eq 9)

	4s @buf memzero
	@buf :0 -> (should eq 0)
	@buf :1 -> (should eq 0)
	@buf :2 -> (should eq 0)
	@buf :3 -> (should eq 0)
])

// ( len src* dst* -- dst* )
(word memcpy ( U16 $0 AnyPtr -- $0 ) [
	(as @U8 @U8) move (r dup)

	dup rot                        // ( src src len      | dst* dst* )
	(sizeof (Child $0)) (as U16) * // ( src src len*sz   | dst* dst* )
	swap + swap                    // ( src*+len*sz src* | dst* dst* )

	(while [ drop 2dup <> ] [
		dup -> (r copy) <-
		1+ (r 1+)
	])

	(r drop)
	drop drop

	(r move) (as $0)
])
(test memcpy [
	(let foo [Char8 11])
	'X 10s @foo + <-

	"Hail FORTH" dup strlen swap @foo memcpy drop
	0s @foo + -> (should eq 'H)
	1s @foo + -> (should eq 'a)
	2s @foo + -> (should eq 'i)
	3s @foo + -> (should eq 'l)
	4s @foo + -> (should eq  0x20)
	5s @foo + -> (should eq 'F)
	6s @foo + -> (should eq 'O)
	7s @foo + -> (should eq 'R)
	8s @foo + -> (should eq 'T)
	9s @foo + -> (should eq 'H)

	10s @foo + -> (should eq 'X)
])

(struct ExpansionCopyCommand
	[type     U8]
	[len     U16]
	[srcbank U16]
	[src @Opaque]
	[dstbank U16]
	[dst @Opaque]
)

// ( len src* dst* -- )
(word memcpyr ( U16 $0 AnyPtr -- ) [
	(let cmdbuf ExpansionCopyCommand [0x02])

	@cmdbuf :dst <-
	@cmdbuf :src <-
	@cmdbuf :len <-

	@cmdbuf .System/expansion deo
])

#noreturn #dampe
(word panic (@U8 -- ) [
	"Panic: " print-string print-string nl
	dampe/backtrace
	"Aborting.\n" print-string
	0x01 .System/state deo
	(asm "" .Op/Obrk)
])

#noreturn
(word panic (@U8 -- ) [
	"Panic: " print-string print-string nl
	.System/rst (asm "" .Op/Odei) 2 /
	(while [ 0> ] [
		"  at " print-string
		(wild ( -- U16) [ (asm "sr" .Op/Osth) ])
			print nl
		1-
	])
	"Aborting.\n" print-string
	0x01 .System/state deo
	drop
	(asm "" .Op/Obrk)
])
